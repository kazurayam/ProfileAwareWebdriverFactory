plugins {
    id 'groovy'
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

group 'com.kazurayam'
version '0.6.4-SNAPSHOT'

ext.isReleaseVersion = ! version.endsWith("SNAPSHOT")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each {
    it.options.encoding = defaultEncoding
}

ext {
    groovyVersion = '2.4.21'
    slf4jVersion = '1.7.25'
    selenium3Version = '3.141.59'
    webdrivermanagerVersion = '5.0.3'
    junitVersion = '4.12'
    subprocessjVersion = '0.1.0'
    gsonVersion = '2.8.2'
    jcommanderVersion = '1.82'
}

configurations {
    generateDocs
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: "${slf4jVersion}"
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: "${selenium3Version}"
    implementation group: 'com.google.code.gson', name: 'gson', version: "${gsonVersion}"
    implementation group: 'com.beust', name: 'jcommander', version: "${jcommanderVersion}"

    testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: "${selenium3Version}"
    testImplementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: "${webdrivermanagerVersion}"
    testImplementation group: 'junit', name: 'junit', version: "${junitVersion}"
    testImplementation group: 'com.kazurayam', name: 'subprocessj', version: "${subprocessjVersion}"
    testImplementation group: 'com.kazurayam', name: 'timekeeper', version: '0.3.3'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

test {
    finalizedBy jacocoTestReport
}

task javadoc(type:Javadoc, overwrite:true) {
    group = 'com.kazurayam'
    source = sourceSets.main.groovy
    classpath = configurations.generateDocs
    //javaClasspath = project.configurations.generateDocs
    include 'com/kazurayam/webdriverfactory/**'
    exclude '**/*Test.groovy'
}

task publishJavadoc(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs/api'
}
groovydoc.finalizedBy publishJavadoc

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        webdriverfactory(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'A Java/Groovy library that enbles launching Selenium WebDriver specifying User Profile'
                url = 'https://kazurayam.github.io/webdriverfactory/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    sign publishing.publications.webdriverfactory
}
// I do not want to sign the SNAPSHOT

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

