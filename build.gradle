plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id 'application'
}

group 'com.kazurayam'
version '0.7.0-SNAPSHOT'

ext.isReleaseVersion = ! version.endsWith("SNAPSHOT")

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each {
    it.options.encoding = defaultEncoding
}

ext {
    groovyVersion = '2.4.21'
    slf4jVersion = '1.7.25'
    seleniumVersion = '3.141.59'
    edgedriverVersion = '4.16.1'
    webdrivermanagerVersion = '5.4.1'
    junitVersion = '4.12'
    subprocessjVersion = '0.3.3'
    gsonVersion = '2.8.2'
    jcommanderVersion = '1.82'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "org.slf4j:slf4j-api:${slf4jVersion}"
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: "${slf4jVersion}"
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-edge-driver', version: "${edgedriverVersion}"
    implementation group: 'org.seleniumhq.selenium', name: 'selenium-java', version: "${seleniumVersion}"
    implementation group: 'com.google.code.gson', name: 'gson', version: "${gsonVersion}"
    implementation group: 'com.beust', name: 'jcommander', version: "${jcommanderVersion}"

    testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testImplementation group: 'io.github.bonigarcia', name: 'webdrivermanager', version: "${webdrivermanagerVersion}"
    testImplementation group: 'junit', name: 'junit', version: "${junitVersion}"
    testImplementation group: 'com.kazurayam', name: 'subprocessj', version: "${subprocessjVersion}"
    testImplementation group: 'com.kazurayam', name: 'timekeeper', version: '0.3.3'
    testImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

test {
    finalizedBy jacocoTestReport
}

application {
    mainClass = 'com.kazurayam.webdriverfactory.CookieServer'
}

javadoc {
    options.encoding ='UTF-8'
    options.locale = "en_US"
}
task copyJavadoc(type: Copy) {
    from 'build/docs/javadoc'
    into 'docs/api'
}
javadoc.finalizedBy copyJavadoc

java {
    withJavadocJar()
    withSourcesJar()
}

artifacts {
    archives javadocJar, sourcesJar
}


publishing {
    publications {
        chromedriverfactory(MavenPublication) {
            pom {
                groupId = project.group
                name = project.rootProject.name
                description = 'A Java/Groovy library that enbles launching Selenium WebDriver specifying User Profile'
                url = 'https://kazurayam.github.io/chromedriverfactory/'
                from components.java
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org.licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kazurayam'
                        name = 'URAYAMA,kazuaki'
                        email = 'kazuaki.urayama@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/kazurayam/${project.rootProject.name}.git"
                    developerConnection = "scm:git:git@github.com:kazurayam/${project.rootProject.name}.git"
                    url = "https://github.com/kazurayam/${project.rootProject.name}"
                }
            }
        }
    }
    repositories {
        maven {
            def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = isReleaseVersion ? releaseRepo: snapshotRepo
            credentials {
                username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
            }
        }
    }
}

signing {
    sign publishing.publications.chromedriverfactory
}
// I do not want to sign the SNAPSHOT

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

